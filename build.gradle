plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.171'
}

version = mcversion + "-" + mod_version + "-" + tag_lanzador
group = mod_group_id

base {
    archivesName = mod_name
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

minecraft {
    accessTransformers {
        file('src/main/resources/META-INF/accesstransformer.cfg')
    }
}

runs {
    configureEach {
        workingDirectory project.file('run')
        
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'
        
        modSource sourceSets.main
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', mod_id
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', mod_id
        programArgument '--nogui'
    }

    gameTestServer {
        systemProperty 'forge.enabledGameTestNamespaces', mod_id
    }

    data {
        workingDirectory project.file('run-data')
        
        programArguments.addAll '--mod', mod_id, 
                                '--all', 
                                '--output', file('src/generated/resources/').getAbsolutePath(), 
                                '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenCentral()
    
    maven {
        name = "Curse Maven"
        url = "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    
   maven {
        url = "https://maven.latvian.dev/releases"
        content {
            includeGroup "dev.latvian.mods"
            includeGroup "dev.latvian.apps"
        }
    } 
}  

dependencies {
    implementation "net.neoforged:neoforge:${neoversion}"
    
    compileOnly("dev.latvian.mods:rhino:$rhino_version")
    implementation ("dev.latvian.mods:kubejs-neoforge:${kubejs_version}")
    
    implementation "curse.maven:mangos-multiblock-library-revived-1406189:7342473"
    
    implementation "curse.maven:jei-238222:7270455"
    implementation "curse.maven:athena-841890:6669522"
    implementation "curse.maven:jade-324717:6853386"
    implementation "curse.maven:pipez-443900:5609056"
    implementation "curse.maven:mekanism-268560:7132168"
    implementation "curse.maven:ctm-267602:5587515"
    implementation "curse.maven:immersive-engineering-231951:6733669"
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
        minecraft_version: mcversion,
        minecraft_version_range: minecraft_version_range,
        neo_version: neoversion,
        neo_version_range: neo_version_range,
        loader_version_range: loader_version_range,
        mod_id: mod_id,
        mod_license: mod_license,
        mod_version: mod_version,
        mod_authors: mod_authors,
        mod_description: mod_description,
        kubejs_version: kubejs_version
    ]
    
    inputs.properties replaceProperties
    
    filesMatching(['META-INF/neoforge.mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
            "Specification-Title"     : mod_name,
            "Specification-Vendor"    : mod_authors,
            "Specification-Version"   : "1",
            "Implementation-Title"    : project.name,
            "Implementation-Version"  : project.version,
            "Implementation-Vendor"   : mod_authors,
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}